# TODO: also include the gatekeeper API, BUT: users should have access to repositories (not ontologies - they belong to repositories)
# TODO: if a method could take very long, make it oneway (=asynchronous)

module org {
module oor {
module persistence {

  typedef sequence<char> Blob;

  typedef string Text;
  typedef sequence<string> TextList;

  struct Iri {
    Text iriText;
  };
  typedef sequence<Iri> IriList;

  struct Symbol {
    Iri iri;
    Text kind;
  };
  typedef sequence<Symbol> SymbolList;

  struct Sentence {
    Iri iri;
    Text form;
  };
  typedef sequence<Sentence> SentenceList;

  struct Language {
    Iri iri;
    Text name;
  };
  typedef sequence<Language> LanguageList;

  struct Logic {
    Iri iri;
    Text name;
  };
  typedef sequence<Logic> LogicList;

  struct Metrics {
    Iri iri;
    Text name;
    Text value;
  };
  typedef sequence<Metrics> MetricsList;

  struct Note {
    Iri iri;
    Iri authorIri;
    Text authorName;
    Iri entryIri;
    Text entryComment;
  };
  typedef sequence<Note> NoteList;

  struct Metadatum {
    Iri propertyIri;
    Iri propertyTypeIri;
    Iri rangeIri;
    Iri rangeTypeIri;
  };
  typedef sequence<Metadatum> MetadatumList;

  struct OntologyFractum { # TM: synchronize this with OMV
    Iri iri;
    MetadatumList metadatumList;
  };
  typedef sequence<OntologyFractum> OntologyFractumList;

  struct OntologyVersionContent {
    Iri iri;
    MetadatumList metadatumList;
    Blob blob;
    SymbolList symbolList;
    SentenceList sentenceList;
  };
  typedef sequence<OntologyVersionContent> OntologyVersionContentList;

  struct OntologyContent {
    OntologyFractum fractum;
    OntologyVersionContent currentVersion;
  };
  typedef sequence<OntologyContent> OntologyContentList;

  struct RepositoryFractum {
    Iri iri;
    Text name;
    OntologyFractumList ontologyFractumList;
  };
  typedef sequence<RepositoryFractum> RepositoryFractumList;

  struct RepositoryContent {
    Iri iri;
    Text name;
    OntologyContentList ontologyContentList;
  };
  typedef sequence<RepositoryContent> RepositoryContentList;

  interface Query {
  };
  interface OrQuery : Query {
    attribute Query operandA;
    attribute Query operandB;
  };
  interface AndQuery : Query {
    attribute Query operandA;
    attribute Query operandB;
  };
  interface NotQuery : Query {
    attribute Query operand;
  };
  interface TypeQuery : Query {
    attribute Iri typeIri;
  };
  interface ItemQuery : Query {
    attribute Iri itemIri;
  };
  interface LabelQuery : Query {
    attribute Text labelText;
  };
  interface PropertyQuery : Query {
    attribute Iri propertyTypeIri;
    attribute Iri rangeTypeIri;
  };
  interface PropertyAscriptionQuery : Query {
    attribute Iri propertyTypeIri;
    attribute Iri rangeTypeIri;
    attribute Iri rangeIri;
  };

  interface Ontology {
    void getMetadatumList(in Iri iri, out MetadatumList metadatumList);
    void setMetadatumList(in Iri iri, in MetadatumList metadatumList);
    void addMetadatum(in Iri iri, in Metadatum metadatum);
    void addMetadatumList(in Iri iri, in MetadatumList metadatumList);
    void delMetadatum(in Iri iri, in Iri metadatumIri);
    void delMetadatumList(in Iri iri, in IriList metadatumIriList);

    void getVersionIriList(in Iri iri, out Iri versionIriList);
    void setVersionIriList(in Iri iri, in Iri versionIriList);
    void addVersionIri(in Iri iri, in Iri versionIri);
    void delVersionIri(in Iri iri, in Iri versionIri);
    void setCurrentVersionIri(in Iri iri, in Iri versionIri);
    void getCurrentVersionIri(in Iri iri, out Iri versionIri);
    void getCurrentVersionContent(in Iri iri, out OntologyVersionContent versionContent);

    void getFractum(in Iri iri, out OntologyFractum fractum);
    void setFractum(in Iri iri, in OntologyFractum fractum);
    void getContent(in Iri iri, out OntologyContent content);
    void setContent(in Iri iri, in OntologyContent content);
  };

  interface OntologyVersion {
    void getMetadatumList(in Iri iri, out MetadatumList metadatumList);
    void setMetadatumList(in Iri iri, in MetadatumList metadatumList);
    void addMetadatum(in Iri iri, in Metadatum metadatum);
    void addMetadatumList(in Iri iri, in MetadatumList metadatumList);
    void delMetadatum(in Iri iri, in Iri metadatumIri);
    void delMetadatumList(in Iri iri, in IriList metadatumIriList);

    void getBlob(in Iri iri, out Blob blob);
    void setBlob(in Iri iri, in Blob blob);

    void getSymbolList(in Iri iri, out SymbolList symbolList);
    void setSymbolList(in Iri iri, in SymbolList symbolList);
    void addSymbol(in Iri iri, in Symbol symbol);
    void delSymbol(in Iri iri, in Iri symbolIri);
 
    void getSentenceList(in Iri iri, out SentenceList sentenceList);
    void setSentenceList(in Iri iri, in SentenceList sentenceList);
    void addSentence(in Iri iri, in Sentence sentence);
    void delSentence(in Iri iri, in Iri sentenceIri);

    void getContent(in Iri iri, out OntologyVersionContent content);
    void setContent(in Iri iri, in OntologyVersionContent content);
  };

  interface Repository {
    void getMetadatumList(in Iri iri, out MetadatumList metadatumList);
    void setMetadatumList(in Iri iri, in MetadatumList metadatumList);
    void addMetadatum(in Iri iri, in Metadatum metadatum);
    void addMetadatumList(in Iri iri, in MetadatumList metadatumList);
    void delMetadatum(in Iri iri, in Iri metadatumIri);
    void delMetadatumList(in Iri iri, in IriList metadatumIriList);

    void getOntologyIriList(in Iri iri, out IriList ontologyIriList);
    void getOntologyFractumList(in Iri iri, out OntologyFractumList ontologyFractumList);
    void getOntologyContentList(in Iri iri, out OntologyContentList ontologyContentList);
    void setOntologyList(in Iri iri, in IriList ontologyIriList);
    void addOntology(in Iri iri, in Iri ontologyIri);
    void delOntology(in Iri iri, in Iri ontologyIri);

    void getFractum(in Iri iri, out RepositoryFractum repositoryFractum);
    void setFractum(in Iri iri, in RepositoryFractum repositoryFractum);
    
    void getContent(in Iri iri, out RepositoryContent repositoryContent);
    void setContent(in Iri iri, in RepositoryContent repositoryContent);
  };

  interface OntologyBase {
# TM: the idea was to have one very powerful method for searching for ontologies
# we have agreed to Todd's suggestion:
# The API for'selecting' is effectively a query mechanism, the front end to 
# the user. The other side of this is the 'retrieving' of the requested or 
# queried data. For the front end of selecting or querying for ontologies 
# (or metadata about them) use of an instance of the metadata ontology should 
# be used as the single parameter for the 'select' method. The particular 
# parameters of interest would be represented by non-null values (in the 
# ontology instance). This simplifies the API and overall architecture 
# (but moves complexity to the parsing of the ontology instance). The 
# retrieval (or return values) of the'selected' results would make use 
# of federate identifiers. 
# the single parameter should be a struct
#
# the user should be able to select whether the method call should just 
# deliver ids (compact representation) or all the metadata (say, as an XML 
# tree), or even the whole ontology/ies (can become very large) 

    void selectOntologyIriList(in Query query, out IriList ontologyIriList);
    void insertOntology(in OntologyContent ontologyContent, out Iri iri);
    void updateOntology(in OntologyContent ontologyContent);
    void removeOntology(in Iri iri);
  };

  interface RepositoryBase {
    void selectRepositoryIriList(in Query query, out IriList repositoryIriList);
    void insertRepository(in RepositoryContent repositoryContent, out Iri iri);
    void updateRepository(in RepositoryContent repositoryContent);
    void removeRepository(in Iri iri);
  };

};
};
};

